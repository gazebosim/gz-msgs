load(
    "//gz_bazel:build_defs.bzl",
    "GZ_ROOT",
    "GZ_VISIBILITY",
    "cmake_configure_file",
    "generate_include_header",
    "generate_yaml",
    "gz_config_header",
    "gz_export_header",
)
load(
    ":gz_msg_gen.bzl",
    "get_proto_headers",
    "gz_msg_gen",
)

package(
    default_visibility = GZ_VISIBILITY,
    features = [
        "-parse_headers",
        "-layering_check",
    ],
)

licenses(["notice"])

exports_files(["LICENSE"])

PROJECT_NAME = "gz-msgs"

PROJECT_MAJOR = 8

PROJECT_MINOR = 0

PROJECT_PATCH = 0

# Generates config.hh based on the version numbers in CMake code.
gz_config_header(
    name = "config",
    src = "include/gz/msgs/config.hh.in",
    cmakelists = ["CMakeLists.txt"],
    project_name = PROJECT_NAME,
    project_version = (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),
)

gz_export_header(
    name = "include/gz/msgs/Export.hh",
    export_base = "GZ_MSGS",
    lib_name = "gz-msgs",
    visibility = ["//visibility:private"],
)

public_headers_no_gen = glob([
    "include/gz/msgs/*.hh",
    "include/gz/msgs/detail/*.hh",
])

protos = glob(["proto/gz/msgs/*.proto"])

generate_include_header(
    name = "messagetypeshh_genrule",
    out = "include/gz/msgs/MessageTypes.hh",
    hdrs = get_proto_headers(protos),
    strip_prefix = ["gz_msgs"],
)

generate_include_header(
    name = "msghh_genrule",
    out = "include/gz/msgs.hh",
    hdrs = public_headers_no_gen + [
        "include/gz/msgs/config.hh",
        "include/gz/msgs/Export.hh",
        "include/gz/msgs/MessageTypes.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/gz/msgs/config.hh",
    "include/gz/msgs/Export.hh",
    "include/gz/msgs/MessageTypes.hh",
    "include/gz/msgs.hh",
]

# Custom Gazebo Protoc plugin
cc_binary(
    name = "gz_msgs_gen",
    srcs = [
        "src/Generator.cc",
        "src/Generator.hh",
        "src/generator_main.cc",
    ],
    deps = [
        "@com_google_protobuf//:protobuf",
        "@com_google_protobuf//:protoc_lib",
    ],
)

# Create a library of our protobuf message files
proto_library(
    name = "gzmsgs_proto",
    srcs = protos,
    strip_import_prefix = "proto",
)

# Create a library of our protobuf message files
proto_library(
    name = "gzmsgs_proto_public",
    srcs = protos,
    strip_import_prefix = "proto",
)

# Generate our custom CC files from the protos
gz_msg_gen(
    name = "gzmsgs_proto_cc",
    deps = [":gzmsgs_proto"],
)

cc_library(
    name = "gz_msgs",
    srcs = [
        "src/Factory.cc",
        "src/Filesystem.cc",
        "src/Utility.cc",
        ":gzmsgs_proto_cc",
    ],
    hdrs = public_headers,
    includes = ["include"],
    deps = [
        ":gzmsgs_proto_cc",
        GZ_ROOT + "gz_math",
        "@com_google_protobuf//:protobuf",
        "@tinyxml2",
    ],
)

# use shared library only when absolutely needd
cc_binary(
    name = "libgz-msgs.so",
    srcs = [
        "src/gz.cc",
        "src/gz.hh",
    ],
    includes = ["include"],
    linkshared = True,
    linkstatic = True,
    deps = [
        ":gz_msgs",
    ],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    data = [GZ_ROOT + "gz_msgs/test:desc/stringmsg.desc"],
    deps = [
        ":gz_msgs",
        GZ_ROOT + "gz_math",
        GZ_ROOT + "gz_msgs/test:test_utils",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in glob(
    [
        "src/*_TEST.cc",
    ],
)]

cmake_configure_file(
    name = "msgs.rb",
    src = "src/cmd/cmdmsgs.rb.in",
    out = "cmdmsgs.rb",
    cmakelists = ["CMakeLists.txt"],
    defines = [
        "library_location=libgz-msgs.so",
        "PROJECT_VERSION_FULL=%d.%d.%d" % (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),  # noqa
        "GZ_LIBRARY_NAME=%s" % [PROJECT_NAME],
    ],
)

CMDS = "    - msg   : Print information about messages."

generate_yaml(
    name = "msgs",
    commands = CMDS,
    library_name = PROJECT_NAME,
    library_version = "%d.%d.%d" % (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),
    ruby_target = "msgs.rb",
)
